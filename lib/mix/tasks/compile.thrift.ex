defmodule Mix.Tasks.Compile.Thrift do
  use Mix.Task
  alias Thrift.Parser.FileGroup

  @moduledoc """
  Compiler task that generates Elixir source files from Thrift schema files
  (`.thrift`).

  When this task runs, it first checks the modification times of all source
  files that were generated by the set of `.thrift` files.  If the generated
  files are older than the `.thrift` file that generated them, this task will
  skip regenerating them.

  ## Command line options

    * `--force` - forces compilation regardless of modification times
    * `--verbose` - enable verbose compile task logging

  ## Configuration

    * `:files` - list of `.thrift` schema files to compile
    * `:output_path` - output directory into which the generated Elixir
      source files will be generated. Defaults to `"lib"`.
  """

  @spec run(OptionParser.argv) :: :ok
  def run(args) do
    {opts, _, _} = OptionParser.parse(args,
      switches: [force: :boolean, verbose: :boolean])

    config      = Keyword.get(Mix.Project.config, :thrift, [])
    files       = Keyword.get(config, :files, [])
    output_path = Keyword.get(config, :output_path, "lib")

    file_groups =
      files
      |> Enum.map(&parse/1)
      |> Enum.reject(&is_nil/1)

    stale_groups = Enum.filter(file_groups, fn file_group ->
      opts[:force] || stale?(file_group, output_path)
    end)

    unless Enum.empty?(stale_groups) do
      File.mkdir_p!(output_path)
      Mix.Utils.compiling_n(length(stale_groups), :thrift)
      Enum.each(stale_groups, &generate(&1, output_path, opts))
    end
  end

  defp parse(thrift_file) do
    try do
      Thrift.Parser.parse_file(thrift_file)
    rescue
      e ->
        Mix.shell.error "Failed to parse #{thrift_file}: #{Exception.message(e)}"
        nil
    end
  end

  defp stale?(%FileGroup{initial_file: thrift_file} = group, output_path) do
    targets =
      group
      |> Thrift.Generator.targets
      |> Enum.map(&Path.join(output_path, &1))
    Mix.Utils.stale?([thrift_file], targets)
  end

  defp generate(%FileGroup{} = group, output_path, opts) do
    Thrift.Generator.generate!(group, output_path)
    if opts[:verbose] do
      Mix.shell.info "Compiled #{group.initial_file}"
    end
  end
end
